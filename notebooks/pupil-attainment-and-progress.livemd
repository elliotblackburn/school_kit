<!-- livebook:{"file_entries":[{"name":"2023-bep-results.csv","type":"attachment"}]} -->

# Process pupil attainment and progress

```elixir
Mix.install(
  [
    {:kino, "~> 0.14"},
    {:school_kit, github: "elliotblackburn/school_kit"}
    # {:school_kit, path: Path.join(__DIR__, ".."), env: :dev},
    # {:school_kit, path: "/home/elliot/code/school_kit", env: :dev},
  ]
  # config_path: :school_kit,
  # lockfile: :school_kit
)
```

## Select input data

```elixir
import Kino.Shorts

cohort_year = read_select("Cohort year", [{nil, "Select cohort year"}, {"2023", "2023"}])

if cohort_year == nil do
  Kino.interrupt!(:error, "You must select a cohort year")
else
  :ok
end
```

```elixir
data_file_input = read_file("Results data", accept: ~w(.csv))

data_file = if data_file_input == nil do
  Kino.interrupt!(:error, "You must select an input file")
else
  Kino.Input.file_path(data_file_input.file_ref)
end
```

## Load and parse data

The data comes to us in a CSV format with a row per student, and a column per facet. These facets include:

* Attributes about the student, such as SEND status
* The school the student is from (for processing multi-academy trusts as a whole)
* Each grade from each subject they sat

`SchoolKit` comes with a parser for a particular CSV format, although it would be possible to write your own. This parser reads in the data, and parses each student record to create an elixir map we can work with.

The parser will also normalise all qualification grades over to the GCSE reformed system, this allows us to compare various subjects, and calcuate Attainment and Progress 8.

The resulting student record is then represented as an elixir map, with atom keys. Each record has only the subjects the student received a grade for, each grade is a floating point number normalised to the GCSE Reformed spec.

```elixir
parsed_results =
  data_file
  |> SchoolKit.Parser.from_csv()
```

## Attainment 8 - How well a student performed in their exams

A students Attainment 8 shows what they managed to achieve in their final GCSE exam results. It is based exclusively on their exam results.

The attainment 8 isn't all that useful by itself, but it feeds into the Progress 8 score which we will calculate after. Attainment 8 is also not something students will think about for themselves, but it helps us to compare cohorts of students, and understands a schools performance.

Attainment 8 is calculated via a series of weighted buckets. Particular subjects fit into different buckets, and some buckets are weighted higher. This calculation and the subject bucket assignments are dictated by the DfE.

### Bucket 1 - English and Maths

This is for English and Maths, and the end result is double weighted. Bucket 1 is calculated by taking the best grade between English Literature and English Language, and adding it to the grade for Maths. For example, a student with English Literature of 4, English Language of 5, and Maths of 4 will get a final Bucket 1 grade of 18. The English Language grade was higher, so that was doubled and added to Maths which was also doubled.

The unused English grade can be re-used in Bucket 3 later on.

### Bucket 2 - English Baccalaureate (EBacc)

Sciences, Computing, Geography, History, and Modern Foreign Languages make up the majority of the English Baccalaureate which is Bucket 2. The top 3 grades from across these subjects are selected, and added together to create the Bucket 2 result. This bucket is single weighted.

### Bucket 3 - Open Subjects (Vocationals)

Vocational qualifications such as BTEC's and anything else fall into Bucket 3. The unused English subject from Bucket 1 is also included here. Like Bucket 2, we take the top three grades and add them together to get the final Bucket 3 result. This bucket is also single weighted.

### Total Attainment 8

The total attainment 8 is simply calculated by adding together the total value from all three buckets. We also calculate a 10 subject average which is used in other calculations later on.

```elixir
results_with_attainment_8 =
  parsed_results
  |> Enum.map(fn student_record ->
    SchoolKit.Attainment8.calculate_attainment_8(student_record)
  end)

IO.puts("Attainment 8 calculated successfully, displaying first student record as an example")
Kino.Tree.new(List.first(results_with_attainment_8))
```

## Progress 8 - The amount of progress a student made during KS3

A students Progress 8 is a representation of how much progress a student made between KS2 and the end of KS3. It is calculated using a lookup data set published by the DfE for each cohort after they've taken their final GCSE exams. This data set defines an expected progress of a student in a subject given a particular KS2 result.

Progress 8 is not something which students themselves ever receive or think about. It is purely to help assess the performance of a school.

Like with Attainment 8, the Progress 8 calculations are given by the DfE.

```elixir
results_with_progress_8 =
  results_with_attainment_8
  |> Enum.map(fn student_record ->
    SchoolKit.Progress8.calculate_progress_8(student_record, cohort_year)
  end)

IO.puts("Progress 8 calculated successfully, displaying first student record as an example")
Kino.Tree.new(List.first(results_with_progress_8))
```

## Results

Now we've completed all our data cleaning and initial calculations, we can output the data into a JSON file. This will maintain the structure we've put together, and make it easy to load into other notebooks for analysis.

```elixir
Kino.Download.new(
  fn ->
    Jason.encode!(results_with_progress_8, pretty: true)
  end,
  filename: "results_data.json",
  label: "Download result data"
)
```

<!-- livebook:{"branch_parent_index":0} -->

## Summary Reports

```elixir
data_file
|> SchoolKit.generate_subject_progress_report_from_csv(cohort_year)
|> Kino.DataTable.new(
  name: "Summary per Subject",
  keys: [
    "Subject",
    "Attainment Avg",
    "Progress Avg",
    "Male Attainment Avg",
    "Male Progress Avg",
    "Female Attainment Avg",
    "Female Progress Avg",
    "Disadvantaged Attainment Avg",
    "Disadvantaged Progress Avg",
    "SEND Attainment Avg",
    "SEND Progress Avg"
  ]
)
```
